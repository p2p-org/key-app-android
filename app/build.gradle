plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.appdistribution'
    id 'com.google.firebase.crashlytics'
    id "io.sentry.android.gradle" version "3.2.0"
    id 'kotlin-kapt'
}

apply from: "$project.rootDir/scripts/ktlint.gradle"
apply from: "$project.rootDir/scripts/versioning.gradle"
apply from: "$project.rootDir/scripts/signing.gradle"
apply from: "$project.rootDir/scripts/config.gradle"
apply from: "$project.rootDir/scripts/analytics.gradle"
apply from: "$project.rootDir/scripts/torus.gradle"

android {
    compileSdkVersion sdkCompileVersion

    defaultConfig { config ->
        applicationId "org.p2p.wallet"
        minSdkVersion sdkMinVersion
        targetSdkVersion sdkTargetVersion
        versionCode VERSION_CODE
        versionName VERSION_NAME

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk { abiFilters("armeabi-v7a", "x86_64", "arm64-v8a", "x86") }

        setProperty("archivesBaseName", generateAppName())
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            shrinkResources false
            versionNameSuffix ".${getVersionBuild()}-debug"
        }

        feature {
            applicationIdSuffix ".feature"
            signingConfig signingConfigs.debug
            debuggable true
            minifyEnabled true
            shrinkResources true
            versionNameSuffix ".${getVersionBuild()}-feature"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            firebaseAppDistribution {
                artifactType = "APK"
                releaseNotes = featureChangelog()
                groups = "wallet.feature.test"
            }

            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
            matchingFallbacks = ["debug"]
        }

        release { buildType ->
            signingConfig signingConfigs.release
            debuggable false
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            firebaseAppDistribution {
                artifactType = "APK"
                releaseNotes = releaseChangelog(VERSION_NAME, buildType.name)
                groups = "wallet.release.test"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = generateAppName() + "-${variant.buildType.name}" + ".apk"
        }
    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests.all { useJUnitPlatform() }
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
        animationsDisabled = true
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    sentry {
        // List the build types that should be ignored
        ignoredBuildTypes = ["debug", "feature"]
        autoInstallation { enabled = false }
    }

    bundle {
        language {
            enableSplit = false
        }
    }
}
dependencies {
    implementation project(':solana')
    implementation project(':ui-kit')
    implementation project(':core')
    implementation files('libs/borshj-0.0.0.jar')

    implementation "androidx.appcompat:appcompat:1.6.0"
    implementation "androidx.biometric:biometric:1.1.0"
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation 'androidx.activity:activity-ktx:1.6.1'
    implementation 'androidx.fragment:fragment-ktx:1.5.5'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation("com.google.android.material:material") {
        version { strictly "1.6.1" }
    }
    implementation "androidx.browser:browser:1.4.0"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    def coroutinesVersion = '1.6.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    def work_version = "2.7.1"
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // https://github.com/JakeWharton/timber
    implementation "com.jakewharton.timber:timber:5.0.1"
    // https://github.com/dm77/barcodescanner
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    // https://github.com/JakeWharton/ThreeTenABP
    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.0'

    def retrofitVersion = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    def okHttpVersion = '4.9.3'
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:31.1.1')

    // Declare the dependencies for the Crashlytics and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-config'

    implementation 'io.intercom.android:intercom-sdk:14.0.0'

    implementation 'com.amplitude:android-sdk:2.35.3'

    implementation "com.google.android.gms:play-services-base:18.1.0"

    // Debug drawer
    def debugDrawerVersion = '0.8.0'
    implementation "io.palaima.debugdrawer:debugdrawer-timber:$debugDrawerVersion"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    implementation 'com.caverock:androidsvg-aar:1.4'

    // Room
    def roomVersion = '2.4.3'
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // Lottie
    def lottieVersion = '4.0.0'
    implementation "com.airbnb.android:lottie:$lottieVersion"

    // Sentry
    def sentryVersion = '6.12.1'
    implementation "io.sentry:sentry-android:$sentryVersion"
    implementation "io.sentry:sentry-android-fragment:$sentryVersion"
    implementation "io.sentry:sentry-android-core:$sentryVersion"
    implementation "io.sentry:sentry-android-okhttp:$sentryVersion"
    implementation "io.sentry:sentry-android-ndk:$sentryVersion"

    // dots indicator
    implementation "com.tbuonomo:dotsindicator:4.3"

    // Google Play Auth
    implementation 'com.google.android.gms:play-services-auth:20.3.0'

    //AppsFlyer
    implementation 'com.appsflyer:af-android-sdk:6.9.0'
    implementation 'androidx.ads:ads-identifier:1.0.0-alpha04'
    implementation "com.android.installreferrer:installreferrer:2.2"

    // TickerView
    implementation 'com.robinhood.ticker:ticker:2.0.4'
    implementation 'androidx.core:core-splashscreen:1.0.0'

    implementation 'io.michaelrocks:libphonenumber-android:8.12.52'

    // core
    testImplementation 'androidx.test:core-ktx:1.4.0'
    testImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    // https://github.com/mockk/mockk
    testImplementation 'io.mockk:mockk:1.12.0'
    // https://mvnrepository.com/artifact/junit/junit
    testImplementation 'junit:junit:4.13.2'
    // https://github.com/robolectric/robolectric
    testImplementation "org.robolectric:robolectric:4.5.1"

    implementation('com.lokalise.android:sdk:2.1.1') {
        transitive = true
    }
    // Coroutines support
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"

    // Koin Test features
    def koinVersion = '3.2.0'
    testImplementation "io.insert-koin:koin-test:$koinVersion"
    testImplementation "io.insert-koin:koin-test-junit4:$koinVersion"

    testImplementation "org.assertj:assertj-core:3.22.0"
    testImplementation 'org.slf4j:slf4j-nop:1.7.30'

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
    testImplementation "org.assertj:assertj-core:3.22.0"

    testRuntimeOnly("org.junit.platform:junit-platform-launcher:1.8.2") {
        because 'This lib comes shipped with the IDE and it possible that newer versions of JUnit 5 maybe be incompatible with the version of junit-platform-launcher shipped with the IDE.'
    }
    def junitJupiterVersion = "5.8.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$junitJupiterVersion"
}