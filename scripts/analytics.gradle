apply plugin: 'com.android.application'

android {
    defaultConfig {
        def sentryProperties = new Properties()
        def sentryPropertiesFile = rootProject.file("sentry.properties")
        if (sentryPropertiesFile.canRead()) {
            logger.quiet "(SENTRY): using sentry properties file"
            sentryProperties.load(new FileInputStream(sentryPropertiesFile))
        } else {
            logger.quiet "(SENTRY): creating sentry properties from CI"
            sentryProperties["defaults.project"] = "android"
            sentryProperties["defaults.org"] = "p2p-wallet"
            sentryProperties["defaults.url"] = "https://sentry.io/"
            sentryProperties["auth.dsn"] = "${System.getenv('SENTRY_AUTH_DSN')}"
            sentryProperties["auth.token"] = "${System.getenv('SENTRY_AUTH_TOKEN')}"
        }

        manifestPlaceholders = [SENTRY_DSN: sentryProperties["auth.dsn"]]
    }

    buildTypes {
        def analyticsProperties = new Properties()
        def analyticsPropertiesFile = rootProject.file("analytics.properties")
        if (analyticsPropertiesFile.canRead()) {
            logger.quiet "(ANALYTICS): using analytics properties file"
            analyticsProperties.load(new FileInputStream(analyticsPropertiesFile))
        } else {
            logger.quiet "(ANALYTICS): creating analytics properties from CI"
            analyticsProperties["amplitudeDebugKey"] = "${System.getenv('AMPLITUDE_KEY_DEBUG')}"
            analyticsProperties["amplitudeReleaseKey"] = "${System.getenv('AMPLITUDE_KEY_RELEASE')}"
        }

        logger.quiet "(ANALYTICS): debug key \"${System.getenv('AMPLITUDE_KEY_DEBUG')}\""
        logger.quiet "(ANALYTICS): debug key from properties ${analyticsProperties["amplitudeDebugKey"]}"

        def debugKey = analyticsProperties["amplitudeDebugKey"]

        debug {
            manifestPlaceholders = [SENTRY_ENV: "debug"]
            buildConfigField("String", "amplitudeKey", debugKey)
        }
        feature {
            manifestPlaceholders = [SENTRY_ENV: "feature"]
            buildConfigField("String", "amplitudeKey", debugKey)
        }
        releaseLocal {
            manifestPlaceholders = [SENTRY_ENV: "release"]
            buildConfigField("String", "amplitudeKey", debugKey)
        }
        releaseFirebase {
            manifestPlaceholders = [SENTRY_ENV: "release"]
            buildConfigField("String", "amplitudeKey", debugKey)
        }
        release {
            manifestPlaceholders = [SENTRY_ENV: "release"]
            buildConfigField("String", "amplitudeKey", analyticsProperties["amplitudeReleaseKey"])
        }
    }
}