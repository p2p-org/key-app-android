def sh = { args -> args.execute().text.trim() }

def gitSha = { sh('git rev-parse --short HEAD') }

def branchFullName = { System.getenv('GITHUB_HEAD_REF') ?: sh('git rev-parse --abbrev-ref HEAD') }

def branchName = { "${branchFullName().split('/').last().toString()}" }

def now = { new Date().format('HH:mm dd.MM.yyyy') }

def changelog = {
    def versionPattern = /(?=##.*)/
    def lines = file("${project.rootDir}/CHANGELOG.md").getText('UTF-8').split(versionPattern)
    if (lines.size() < 2) return "Changelog not found or has wrong format"
    return lines[1]
}

def branchChangelog = { sh("git log origin/${branchFullName()} --not origin/develop --pretty=format:%s --no-merges -n 100") }

ext.developChangelog = {
    return "Commit: ${gitSha()}\n" +
            "Time: ${now()}\n" +
            "Release Notes:\n" +
            changelog()
}

ext.featureChangelog = {
    // getting the last commit's message from develop branch
    def developBranchChangelog = sh("git log -1 --pretty=format:'%b' develop")

    return "Branch: ${branchName()}\n" +
            "Commit: ${gitSha()}\n" +
            "Time: ${now()}\n" +
            "Release Notes:\n" +
            developBranchChangelog
}

ext.releaseChangelog = { releaseName, buildType ->
    new StringBuilder()
            .append("Release build v${releaseName}\n")
            .append("Type=$buildType\n")
            .append("Release notes:\n")
            .append("${branchChangelog()}")
            .toString()
}

task gitChangelogTask(type: Exec) {
    def from = findProperty('fromCommit')
    def to = findProperty('toCommit')

    executable "sh"
    args "-c", "git log $from..$to --no-merges --pretty=format:\"%h - %s - %an\" > CHANGELOG.txt"

    doFirst {
        if (from == null || to == null) {
            throw new GradleException("fromCommit or toCommit is not defined. Did you set properties? (-PfromCommit=somecommithash -PtoCommit=somecommithash)")
        }
    }
}

task releaseChangelogTask(type: Exec) {
    def currentVersionName = Versions.INSTANCE.VERSION_NAME
    def changelogFile = file('CHANGELOG.txt')

    doFirst {
        // Create changelog directory if it doesn't exist
        changelogFile.parentFile.mkdirs()

        // Create CHANGELOG.txt file if it doesn't exist
        if (!changelogFile.exists()) {
            changelogFile.createNewFile()
        }
    }

    doLast {
        changelogFile.withWriter('UTF-8') { writer ->
            writer.writeLine("Version $currentVersionName")
            writer.writeLine("Release")
        }
    }

    commandLine "git", "commit", "-a", "-m", "Updated changelog for version $currentVersionName"
}