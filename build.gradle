buildscript {
    apply from: "$project.rootDir/.scripts/dependencies.gradle"
    apply from: "$project.rootDir/.scripts/changelog.gradle"

    repositories {
        google()
        mavenCentral()
        maven { url "https://maven.google.com/" }
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.5'
        classpath 'com.google.gms:google-services:4.3.15'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url "https://maven.google.com/" }
        maven { url "https://maven.lokalise.com" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task installGitHook(type: Copy) {
    def useGitHooks = use_git_hooks
    if (rootProject.file('local.properties').exists()) {
        def localProperties = new Properties()
        localProperties.load(new FileInputStream(rootProject.file('local.properties')))
        useGitHooks = localProperties.getOrDefault('use_git_hooks', use_git_hooks)
    }

    logger.quiet "(GIT_HOOKS): use_git_hooks=$useGitHooks"
    if (useGitHooks) {
        from rootProject.file('scripts/pre-commit')
        into rootProject.file('.git/hooks')
        fileMode 0777
    } else {
        rootProject.file('.git/hooks/pre-commit').delete()
    }
}

tasks.getByPath(':app:preBuild').dependsOn installGitHook